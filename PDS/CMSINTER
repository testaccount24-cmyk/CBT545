         TITLE 'PL360 INTERFACE ROUTINE'
*"The source code for this computer program is placed in the public
* domain and may be used by any party without notice to the copyright
* holder, Stanford University. Stanford University provides no
* support of any kind to this computer program.  Further:
*
* STANFORD MAKES NO REPRESENTATIONS OR WARRANTIES, EXPRESS OR IMPLIED.
* BY WAY OF EXAMPLE, BUT NOT LIMITATION, STANFORD MAKES NO
* REPRESENTATIONS OR WARRANTIES OF MERCHANTABILITY OR FITNESS FOR ANY
* PARTICULAR PURPOSE OR THAT THE USE OF THE SOFTWARE WILL NOT INFRINGE
* ANY PATENTS, COPYRIGHTS, TRADEMARKS, OR OTHER RIGHTS. STANFORD SHALL
* NOT BE LIABLE FOR ANY LIABILITY OR DAMAGES WITH RESPECT TO ANY CLAIM
* BY A USER OF THIS COMPUTER PROGRAM OR ANY DERIVATIVES OF THIS
* COMPUTER PROGRAM."
*
*
*    REGISTER USAGE:
*
*        R0  : BASE REGISTER FOR DSECT NUCON
*        R1  : WORK REGISTER
*        R2  : ADDRESS OF OLD PLIST
*        R3  : POINTER INTO OLD PLIST
*        R4  : POINTER INTO NEW PLIST
*        R5  : LENGTH OF NEW PLIST
*        R6  : NUMBER OF FILE NAMES IN OLD PLIST
*        R7  : WORK REGISTER
*        R8  : WORK REGISTER
*        R9  : WORK REGISTER
*        R10 : WORK REGISTER
*        R11 : SECOND BASE REGISTER FOR CSECT
*        R12 : FIRST BASE REGISTER FOR CSECT
*        R13 : ADDRESS OF SAVE AREA FOR CSECT
*        R14 : WORK REGISTER
*        R15 : WORK REGISTER
*
*
         MACRO
         ENTER     &SECT=MAIN,&SAVE=SAVEMAIN,&BASEREG=3
&SECT    CSECT
         REGEQU
*
*   AUTHOR:  MIKE CAMPBELL
*
         STM       R14,R12,12(R13)        --SAVE ALL REGISTERS
         LR        R12,R15
         USING     &SECT,R12              --SET THE CSECT BASE REG.
         AIF       (&BASEREG LT 2).LINKSAV
         LA        R11,4095(,R12)         --SET THE SECOND
         LA        R11,1(,R11)              BASE REGISTER FOR
         USING     &SECT+4096,R11           CSECT
         AIF       (&BASEREG LT 3).LINKSAV
         LA        R10,4095(,R11)         --SET THE THIRD
         LA        R10,1(,R10)              BASE REGISTER FOR
         USING     &SECT+8192,R10           CSECT
.LINKSAV LR        R2,R13
         LA        R13,&SAVE              --POINT TO NEW SAVE AREA
         ST        R2,4(,R13)             --LINK NEW SAVE TO OLD
         ST        R13,8(,R2)             --LINK OLD SAVE TO NEW
         MEND
         EJECT
         ENTER SECT=PL360CMS,BASEREG=2,SAVE=SAVEMAIN
         TITLE 'INITIAL SAVES AND CHECKS'
         USING NUCON,R0           BASE REGISTER FOR DSECT NUCON
         LR    R2,R1              SAVE OLD PLIST ADDRESS
         LA    R3,8(R2)           SKIP OVER CALLER'S NAME
         CLI   0(R3),C'?'         NEED HELP ?
         BE    HELP               POOR GUY
         CLI   0(R3),X'FF'        END OF OLD PLIST ?
         BE    ERR001             BAD NEWS GUY, YOU FORGOT A FILE NAME
         CLI   0(R3),C'('         BEGINNING OF OPTIONS ?
         BE    ERR001             TWIT, NO FILE NAME GIVEN
         TITLE 'SEARCH FOR OPTIONS'
         LA    R4,BUFF+1          POINT INTO NEW PLIST
         SR    R5,R5              LENGTH OF NEW PLIST
         LA    R6,1               ONE FILE NAME FOUND IN PLIST SO FAR
FINDOPTN DS    0H
         LA    R3,8(R3)           NEXT TOKEN
         CLI   0(R3),C'('         BEGINNING OF OPTIONS ?
         BE    OPTION             YES, GO SEARCH THEM
         CLI   0(R3),X'FF'        NO PARMS ?
         BE    GOTOPTNS           LESS WORK FOR US TO DO
         LA    R6,1(R6)           COUNT NUMBER OF FILE NAMES GIVEN
         B     FINDOPTN           KEEP SEARCHING FOR OPTIONS
         TITLE 'CHECK IF OPTION IS VALID'
OPTION   DS    0H
         LA    R3,8(R3)           SKIP TO NEXT PARM
         CLI   0(R3),X'FF'        END OF PARMS ?
         BE    GOTOPTNS           IT'S ABOUT TIME TOO
         CLI   0(R3),C')'         END OF OPTIONS ?
         BE    GOTOPTNS           YES
         LA    R8,(OPTEND-OPTIONS)/12  NUMBER OF VALID OPTIONS
         LA    R7,OPTIONS         POINT TO OPTIONS
         SR    R9,R9              CLEAR WORK REGISTER
CHEKOPTN DS    0H
         IC    R9,8(,R7)          MINIMUM OPTION LENGTH
         EX    R9,CLCOPT          IS THIS THE OPTION ?
         BE    GOODOPTN           SURE THING
         LA    R7,12(R7)          IF NOT, TRY THE NEXT OPTION
         BCT   R8,CHEKOPTN        SEE IF THAT IS THE ONE
         B     ERR002             IT'S AN INVALID OPTION TWIT
GOODOPTN DS    0H
         TM    10(R7),PARMOPT     DOES THIS OPTION GO INTO THE PARM ?
         BNO   CMSOPTN            NO - SEE IF CMS OPTION
         IC    R9,9(,R7)          REAL LENGTH OF OPTION
         MVI   0(R4),C','         PLACE OPTION DELIMETER IN NEW PLIST
         LA    R4,1(R4)           BUMP NEW PLIST POINTER
         LA    R5,1(R5)           INCREMENT LENGTH OF NEW PLIST
         EX    R9,MVCOPT          MOVE FULL OPTION TO NEW PLIST
         AR    R5,R9              NEW LENGTH OF NEW PLIST
         AR    R4,R9              BUMP NEW PLIST POINTER
         B     OPTION             LOOK AT NEXT OPTION IN OLD PLIST
CMSOPTN  DS    0H
         TM    10(R7),CMSOPT      IS THIS A CMS OPTION ?
         BZ    OPTION             NO - GET NEXT OPTION IN PLIST
         IC    R9,11(,R7)         OBTAIN FLAG VALUE
         TM    10(R7),SETOPT      IS FLAG TO BE SET ?
         BO    CMSOPSET           YES
         EX    R9,OPRESET         RESET OPTION FLAG
         B     OPTION
CMSOPSET DS    0H
         EX    R9,OPSET           SET OPTION FLAG
         B     OPTION
         SPACE
CLCOPT   CLC   0(*-*,R3),0(R7)
MVCOPT   MVC   0(*-*,R4),0(R7)
OPRESET  NI    CMSFLAGS,*-*
OPSET    OI    CMSFLAGS,*-*
         TITLE 'ALL OPTIONS OBTAINED FROM OLD PLIST'
GOTOPTNS DS    0H
         LTR   R5,R5              ANY OPTIONS IN NEW PLIST ?
         BZ    NOPTIONS           NO, BUT THAT'S ALRIGHT
         BCTR  R5,0               IGNORE FIRST PARM DELIMETER LENGTH
NOPTIONS DS    0H
         STH   R5,BUFF            STORE NEW PLIST LENGTH INTO NEW PLIST
         TITLE 'LOAD PL360 COMPILER AND COMPILE SOURCE'
         LA    R3,8(R2)           POINT TO FILE NAMES IN OLD PLIST
COMPILE  DS    0H
         MVC   STATEFN(8),0(R3)   FILE NAME FOR STATE
         LA    R1,STATE           DO STATE ON SOURCE
         SVC   SVC202
         DC    AL4(ERR003)        IF SOURCE FILE NON-EXISTENT
         L     R1,STATEFST             GET THE FST
         USING FSTSECT,R1
         MVC   SYSINFM(2),FSTM    GUARANTEE RIGHT MODE LET & NUM
         TM    FSTFB,FSTFRW       READ-WRITE DISK ?
         BNO   MOVEFN             NO - LEAVE REST AS MODE A
         MVC   SYSGOFM(1),FSTM    SET MODES = INPUT
         MVC   SYSPRFM(1),FSTM
         DROP  R1
MOVEFN   DS    0H
         MVC   SYSINFN(8),0(R3)   FILE NAME FOR SYSIN
         MVC   SYSGOFN(8),0(R3)   FILE NAME FOR SYSGO
         MVC   SYSPRFN(8),0(R3)   FILE NAME FOR SYSPRINT
         LA    R1,SYSIN           ISSUE SYSIN FILEDEF
         SVC   SVC202
         DC    AL4(ERR004)        IN CASE OF ERROR
         LA    R1,SYSGO           ISSUE SYSGO FILEDEF
         SVC   SVC202
         DC    AL4(ERR004)        IN CASE OF ERROR
         LA    R1,SYSPUNCH        ISSUE SYSPUNCH FILEDEF
         SVC   SVC202
         DC    AL4(ERR004)        IN CASE OF ERROR
         LA    R1,SYSPRNTP        POINT TO SYSPRINT PRINTER FILEDEF
         TM    CMSFLAGS,DISKOUT   IS OUTPUT TO DISK ?
         BZ    *+8                NO
         LA    R1,SYSPRNTD        ISSUE SYSPRINT FILEDEF
         SVC   SVC202
         DC    AL4(ERR004)        IN CASE OF ERROR
         COMPSWT ON               SET COMPILER SWITCH ON
         GETMAIN EC,A=PATHADDR,LV=PATHSIZE
         LTR   R15,R15            DID WE GET IT ?
         BNZ   ERR006             NO - GIVE ERROR ABOUT BIG CALLS
         LA    R1,PLIST           NEW PLIST
         LINK  EP=PL360VM         GO TO PL360 COMPILER
         LR    R2,R15
         FREEMAIN R,LV=PATHSIZE,A=PATHADDR
         LTR   R15,R2             ZERO RETURN CODE ?
         BNZ   ERR005             NO - GO SEE WHAT RETURN CODE MEANS
COMPEND  DS    0H
         COMPSWT OFF              SET COMPILER SWITCH OFF
         LA    R1,FINIS           CLOSE ALL FILES
         SVC   SVC202
         DC    AL4(*+4)           NO PROBLEM WITH ERROR CONDITION
NEXTCOMP DS    0H
         LA    R3,8(R3)           NEXT FILE NAME
         BCT   R6,COMPILE         GO BACK FOR NEXT COMPILE
         TITLE 'RETURN SEQUENCE'
RETURN   DS    0H
         LA    R1,FILCLEAR    TO CLEAR FILEDEFS
         MVC   DD2CLEAR,SYSGO+8
         SVC   SVC202
         DC    AL4(*+4)           IGNORE ERROR
         MVC   DD2CLEAR,SYSPUNCH+8
         SVC   SVC202
         DC    AL4(*+4)           IGNORE ERROR
         MVC   DD2CLEAR,SYSIN+8
         SVC   SVC202
         DC    AL4(*+4)           IGNORE ERROR
         MVC   DD2CLEAR,SYSPRNTD+8
         SVC   SVC202
         DC    AL4(*+4)           IGNORE ERROR
         L     R15,RETCODE        GET RETURN CODE
         L     R13,SAVEMAIN+4     GET OLD SAVEAREA ADDRESS
         L     R14,12(R13)        RESTORE REGISTERS
         LM    R0,R12,20(R13)
         BR    R14                GO BACK TO MAMA
         TITLE 'DOCUMENTATION'
HELP     DS    0H
         WRTERM ' '
         WRTERM 'FORMAT:  PL360   FN1 FN2 .... FNn ( OPTN1 ... OPTNn'
         WRTERM ' '
         WRTERM ' FN: The file name of the  PL360  source file.'
         WRTERM ' '
         WRTERM ' OPTIONS:  LIst      NOLIst'
         WRTERM '           LOad      NOLOad'
         WRTERM '           DEck      NODEck'
         WRTERM '           Print     NOPrint'
         WRTERM '           TErm      NOTErm'
         WRTERM '           DIsk      NODIsk'
         WRTERM ' '
         B     RETURN
         TITLE 'ERROR ROUTINES'
ERR001   DS    0H
         WRTERM '****** NO FILE NAME GIVEN ******'
         B     RETURN
ERR002   DS    0H
         LINEDIT TEXT='****** INVALID OPTION ''........'' ******',     *
               SUB=(CHARA,(R3)),DOT=NO
         B     RETURN
ERR003   DS    0H
         LINEDIT TEXT='****** FILE ''....................'' NOT FOUND **
               *****',SUB=(CHAR8A,STATEFN),DOT=NO
         B     COMPEND
ERR004   DS    0H
         LA    R2,8(,R1)               POINT TO DDNAME
         LINEDIT TEXT='****** ERROR ISSUING FILEDEF FOR ''........'' ***
               ****',SUB=(CHARA,(R2)),DOT=NO
         B     COMPEND
ERR005   DS    0H
         C     R15,RETCODE        BIGGER THAN BIGGEST ?
         BNH   COMPEND            NO - TRUCK ON
         ST    R15,RETCODE        STORE RETURN CODE
         B     COMPEND            GO CLOSE FILES
ERR006   DS    0H
         WRTERM '****** INSUFFICIENT MAIN STORAGE FOR PL360VM ******'
         LA    R6,1               PROCESS NO MORE FILES
         B     COMPEND            RETURN TO CLOSE FILES
         TITLE 'PRINTER AUXILIARY PROCESSOR'
PRINTAUX DS    0H
         USING PRINTAUX,R15
         STM   R0,R15,REGSAVE     SAVE REGISTERS
         LR    R12,R15            LOAD BASE REGISTER
         DROP  R15
         USING PRINTAUX,R12
         USING FCBSECT,R11
         TM    CMSFLAGS,TERMOUT   ARE TERMINAL DIAGNOSTICS WANTED ?
         BZ    AUXEXIT            NO - EXIT
         L     R5,FCBBUFF         OBTAIN BUFFER ADDRESS
         CLI   0(R5),C'1'         IS THIS A SKIP TO A NEW PAGE ?
         BE    AUXEXIT            YES - EXIT
         CLC   =C'ERROR NO',3(R5) IS THIS AN ERROR MESSAGE ?
         BE    AUXTYPE            YES - TYPE NEXT LINE AND MESSAGE
         CLC   =C'********',112(R5)    IS THIS AN ERROR MESSAGE ?
         BE    AUXTYPE                 YES - TYPE NEXT LINE AND MESSAGE
         CLC   =C'***',1(R5)      IS THIS AN ERROR SUMMARY RECORD ?
         BE    AUXTYPLN           YES - TYPE IT
         CLC   =C'        ',1(R5) IS THIS A SOURCE RECORD ?
         BE    AUXEXIT            NO - EXIT
         TM    CMSFLAGS,ERRORFLG  WAS AN ERROR DETECTED?
         BNO   AUXEXIT            NO - EXIT
         MVC   SAVELINE,35(R5)    SAVE IT
         MVC   SAVESEQ,112(R5)    SAVE SEQUENCE NUMBER
         WRTERM SAVEDREC,80       DISPLAY IT.
         NI    CMSFLAGS,255-ERRORFLG   RESET ERROR FLAG
         B     AUXEXIT
AUXTYPLN DS    0H
         LA    R5,1(,R5)          POINT PAST CARRIAGE CONTROL
         WRTERM (R5),80           DISPLAY MESSAGE
         B     AUXEXIT
AUXTYPE  DS    0H
         OI    CMSFLAGS,ERRORFLG  INDICATE ERROR DETECTED
         LA    R5,1(,R5)          POINT PAST CARRIAGE CONTROL
         WRTERM (R5),34           DISPLAY ERROR MESSAGE
         MVC   SAVELINE,34(R5)    CONSTRUCT ERROR LINE
         MVC   SAVESEQ,=CL8' '
         WRTERM SAVEDREC,80       DISPLAY ERROR FLAG
AUXEXIT  DS    0H
         LM    R0,R14,REGSAVE     RESTORE REGISTERS
         SR    R15,R15            SET RETURN CODE
         BR    R14                RETURN TO CALLER
         TITLE 'STORAGE'
SAVEMAIN DS    18F                SAVEAREA
         SPACE
REGSAVE  DS    16F                AUXPROC ROUTINE SAVE AREA
         SPACE
PLIST    DC    X'80',AL3(BUFF)    NEW PLIST FOR COMPILER
         SPACE
BUFF     DS    0H
         DC    H'0',78X'40'
         SPACE
RETCODE  DC    F'0'               RETURN CODE
         SPACE
OPTIONS  DS    0H                 OPTION;MINIMUM LENGTH;MAXIMUM LENGTH
         DC    CL8'DECK',AL1(2,4),AL1(PARMOPT,0)
         DC    CL8'LIST',AL1(2,4),AL1(PARMOPT,0)
         DC    CL8'LOAD',AL1(2,4),AL1(PARMOPT,0)
         DC    CL8'PRINT',AL1(1,5),AL1(PARMOPT,0)
         DC    CL8'DISK',AL1(2,4),AL1(CMSOPT+SETOPT,DISKOUT)
         DC    CL8'TERM',AL1(2,4),AL1(CMSOPT+SETOPT,TERMOUT)
         DC    CL8'NODECK',AL1(4,6),AL1(PARMOPT,0)
         DC    CL8'NOLIST',AL1(4,6),AL1(PARMOPT,0)
         DC    CL8'NOLOAD',AL1(4,6),AL1(PARMOPT,0)
         DC    CL8'NOPRINT',AL1(3,7),AL1(PARMOPT,0)
         DC    CL8'NODISK',AL1(4,6),AL1(CMSOPT,255-DISKOUT)
         DC    CL8'NOTERM',AL1(4,6),AL1(CMSOPT,255-TERMOUT)
OPTEND   DS    0F
         SPACE
PARMOPT  EQU   X'80'              OPTION GOES INTO OS PARM FIELD
CMSOPT   EQU   X'40'              OPTION IS FOR CMS INTERFACE
SETOPT   EQU   X'20'              OPTION SETS FLAG FOR CMS INTERFACE
         SPACE
CMSFLAGS DC    AL1(TERMOUT+DISKOUT)    CMS OPTIONS
TERMOUT  EQU   X'80'                        DISPLAY ERRORS AT TERMINAL
DISKOUT  EQU   X'40'                        PUT LISTING ON DISK
ERRORFLG EQU   X'01'                        ERROR HAS OCCURRED
         SPACE
STATE    DS    0D                 SEE IF FILE EXISTS
         DC    CL8'STATE'
STATEFN  DC    CL8'*'
         DC    CL8'PL360'
         DC    CL2'*'
         DS    H
STATEFST DS    A
         DC    8X'FF'
FINIS    DS    0D                 CLOSE ALL FILES
         DC    CL8'FINIS'
         DC    CL8'*'
         DC    CL8'*'
         DC    CL8'*'
         DC    8X'FF'
FILCLEAR DS     0D
         DC    CL8'FILEDEF'
DD2CLEAR DC    CL8'*'
         DC    CL8'CLEAR'
         DC    8X'FF'
SYSIN    DS    0D
         DC    CL8'FILEDEF'
         DC    CL8'SYSIN'
         DC    CL8'DISK'
SYSINFN  DC    CL8'*'
         DC    CL8'PL360'
SYSINFM  DC    CL8'A'
         DC    CL8'('
         DC    CL8'RECFM'
         DC    CL8'FB'
         DC    CL8'LRECL'
         DC    CL8'80'
         DC    CL8'BLOCK'
         DC    CL8'800'
         DC    CL8'NOCHANGE'
         DC    8X'FF'
SYSGO    DS    0D
         DC    CL8'FILEDEF'
         DC    CL8'SYSGO'
         DC    CL8'DISK'
SYSGOFN  DC    CL8'*'
         DC    CL8'TEXT'
SYSGOFM  DC    CL8'A'
         DC    CL8'('
         DC    CL8'RECFM'
         DC    CL8'FB'
         DC    CL8'LRECL'
         DC    CL8'80'
         DC    CL8'BLOCK'
         DC    CL8'800'
         DC    CL8'NOCHANGE'
         DC    8X'FF'
SYSPUNCH DS    0D
         DC    CL8'FILEDEF'
         DC    CL8'SYSPUNCH'
         DC    CL8'PUNCH'
         DC    CL8'('
         DC    CL8'RECFM'
         DC    CL8'F'
         DC    CL8'LRECL'
         DC    CL8'80'
         DC    CL8'BLOCK'
         DC    CL8'80'
         DC    CL8'NOCHANGE'
         DC    8X'FF'
SYSPRNTD DS    0D
         DC    CL8'FILEDEF'
         DC    CL8'SYSPRINT'
         DC    CL8'DISK'
SYSPRFN  DC    CL8'*'
         DC    CL8'LISTING'
SYSPRFM  DC    CL8'A'
         DC    CL8'('
         DC    CL8'RECFM'
         DC    CL8'FA'
         DC    CL8'LRECL'
         DC    CL8'133'
         DC    CL8'BLOCK'
         DC    CL8'133'
         DC    CL8'NOCHANGE'
         DC    CL8'AUXPROC'
         DC    AL4(PRINTAUX,0)
         DC    8X'FF'
SYSPRNTP DS    0D
         DC    CL8'FILEDEF'
         DC    CL8'SYSPRINT'
         DC    CL8'PRINTER'
         DC    CL8'('
         DC    CL8'RECFM'
         DC    CL8'FA'
         DC    CL8'LRECL'
         DC    CL8'133'
         DC    CL8'BLOCK'
         DC    CL8'133'
         DC    CL8'NOCHANGE'
         DC    CL8'AUXPROC'
         DC    AL4(PRINTAUX,0)
         DC    8X'FF'
SVC202   EQU   202
         SPACE ,
PATHADDR DS    A
PATHSIZE EQU   64*1024
         SPACE
SAVEDREC DS    0CL80
SAVESEQ  DS    CL8
SAVELINE DS    CL72
         TITLE 'LITERAL POOL'
         LTORG
         SPACE 3
*        ROUND UP TO THE NEXT PAGE BOUNDARY
         SPACE 1
         ORG   PL360CMS+((*-PL360CMS+4096)/4096)*4096
         TITLE 'SIMULATED OS CONTROL BLOCKS'
         CMSCB
         TITLE 'CMS DSECT AREAS'
         FSTB
         NUCON
         END       PL360CMS
