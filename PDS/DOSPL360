PLIO     TITLE   'DOS PL360 I/O ROUTINES - O/S COMPATIBLE'
*"The source code for this computer program is placed in the public
* domain and may be used by any party without notice to the copyright
* holder, Stanford University. Stanford University provides no
* support of any kind to this computer program.  Further:
*
* STANFORD MAKES NO REPRESENTATIONS OR WARRANTIES, EXPRESS OR IMPLIED.
* BY WAY OF EXAMPLE, BUT NOT LIMITATION, STANFORD MAKES NO
* REPRESENTATIONS OR WARRANTIES OF MERCHANTABILITY OR FITNESS FOR ANY
* PARTICULAR PURPOSE OR THAT THE USE OF THE SOFTWARE WILL NOT INFRINGE
* ANY PATENTS, COPYRIGHTS, TRADEMARKS, OR OTHER RIGHTS. STANFORD SHALL
* NOT BE LIABLE FOR ANY LIABILITY OR DAMAGES WITH RESPECT TO ANY CLAIM
* BY A USER OF THIS COMPUTER PROGRAM OR ANY DERIVATIVES OF THIS
* COMPUTER PROGRAM."
         SPACE
         MACRO
&EP      ENTER
         ENTRY   &EP
         USING   &EP,15
&EP      STM     12,3,SAVE             SAVE REGISTERS
         L       12,=A($PL360IO)       ESTABLISH ADDRESSING
         USING   $PL360IO,12
         DROP    15
         MEND
         SPACE
         MACRO
&L       EXIT
&L       LM      12,3,SAVE             RESTORE REGISTERS
         BR      14
         DROP    12
         MEND
         SPACE   2
$PL360IO CSECT
         SPACE
LINELEN  EQU     132                   PRINTER LINE LENGTH
LINESMAX EQU     66                    PRINTER LINES/PAGE
         SPACE
*   GLOBAL PROCEDURE READ(R14)
*        (R0)  = BUFFER ADDRESS
*        (R13) = SAVE AREA ADDRESS
*        (R14) = RETURN ADDRESS
READ     ENTER
         CLI     RDOPEN,X'FF'          TEST FOR OPEN DTF
         BE      READ1
         LR      2,0
         MVI     RDOPEN,X'FF'
         OPEN    SYSIN
         LR      0,2
READ1    CLI     EOF,X'FF'             TEST FOR PREVIOUS END-OF-FILE
         BNE     GET1
         DUMP
GET1     GET     SYSIN,(0)
READ2    CLI     EOF,0                 SET CONDITION CODE FOR RETURN
         EXIT
         SPACE
*                  EOD EXIT ROUTINE                                   *
         USING   $PL360IO,12
ENDRDR   MVI     EOF,X'FF'             EODAD EXIT
         B       READ2
         DROP    12
         SPACE
*   GLOBAL PROCEDURE WRITE(R14)
*        (R0)  = BUFFER ADDRESS  (132 CHARACTERS)
*        (R13) = SAVE AREA ADDRESS
*        (R14) = RETURN ADDRESS
WRITE    ENTER
         LR      2,0
         CLI     WTOPEN,X'FF'          TEST IF OPEN
         BE      WRITE0
         MVI     WTOPEN,X'FF'
         OPEN    SYSOUT
         ST    3,SYSOUTLC
WRITE0   CLI     LINECNT,6
         BNL     WRITE1
         MVI     CARRCONT,C'1'
WRITE1   L     3,SYSOUTLC
         MVC     0(1,3),CARRCONT       SUPPLY CONTROL CHARACTER
         CLI     CARRCONT,C'1'
         BNE     WRITE2
         MVI     LINECNT,6             RESET LINE COUNT AND SPACING
         MVI     CARRCONT,C' '
WRITE2   MVC     1(LINELEN,3),0(2)     TRANSFER BUFFER
         PUT   SYSOUT
         ST    3,SYSOUTLC
         IC      2,LINECNT             INCREMENT LINE COUNT
         LA      2,1(,2)
         STC     2,LINECNT
         CLI     LINECNT,LINESMAX      TEST FOR FULL PAGE
         BL      WRITE3
         MVI     CARRCONT,C'1'         SET SKIP
WRITE3   EXIT
         SPACE   2
*   GLOBAL PROCEDURE PAGE(R14)
*        (R14) = RETURN ADDRESS
         ENTRY   PAGE
         USING   PAGE,15
PAGE     MVI     CARRCONT,C'1'         SET PAGE EJECT
         BR      14
         SPACE   2
*   GLOBAL PROCEDURE PUNCH(R14)
*        (R0)  = BUFFER ADDRESS
*        (R13) = SAVE AREA ADDRESS
*        (R14) = RETURN ADDRESS
PUNCH    ENTER
         CLI     PNOPEN,X'FF'
         BE      PUNCH1
         LR      2,0
         MVI     PNOPEN,X'FF'
         OPEN    SYSPNCH
         LR      0,2
PUNCH1   PUT     SYSPNCH,(0)
         EXIT
         SPACE   2
*  GLOBAL PROCEDURE CANCEL (R14);
CANCEL   ENTER
         CANCEL
         DROP  12
*  GLOBAL PROCEDURE  PRINT (R14)
*      (R0) = BUFFER ADDRESS  (133 CHARACTERS)
*      (R13) = SAVE AREA ADDRESS
*      (R14) = RETURN ADDRESS
         SPACE
PRINT    ENTER
         LR      2,0
         CLI     WTOPEN,X'FF'
         BE      PRINT1
         MVI     WTOPEN,X'FF'
         OPEN    SYSOUT
         ST    3,SYSOUTLC
PRINT1   L     3,SYSOUTLC
         MVC     0(133,3),0(2)
         PUT   SYSOUT
         ST    3,SYSOUTLC
         SPACE
*  CARRCONT RESULTING FROM A CALL ON PAGE DOES NOT AFFECT PRINT
         SPACE
         IC      2,LINECNT
         LA      2,1(,2)
         STC     2,LINECNT
         CLI     LINECNT,LINESMAX      TEST FOR FULL PAGE
         BL      PRINT3
         MVI     LINECNT,X'00'
PRINT3   EXIT
RDOPEN   DC      X'00'
WTOPEN   DC      X'00'
PNOPEN   DC      X'00'
EOF      DC      X'00'
LINECNT  DC      X'03'
CARRCONT DC      C' '
SAVE     DS      8F
SYSIN    DTFCD   DEVADDR=SYSIPT,                                       X
               EOFADDR=ENDRDR,                                         X
               IOAREA1=CARDIN1,                                        X
               IOAREA2=CARDIN2,                                        X
               RDONLY=YES,                                             X
               MODNAME=IJCFZHB0,                                       X
               WORKA=YES
IJCFZHB0 CDMOD IOAREA2=YES,                                            X
               RDONLY=YES
CARDIN1  DS      80C
CARDIN2  DS      80C
CARDOT1  DS      80C
CARDOT2  DS      80C
SYSPNCH  DTFCD   DEVADDR=SYSPCH,                                       X
               TYPEFLE=OUTPUT,                                         X
               IOAREA1=CARDOT1,                                        X
               IOAREA2=CARDOT2,                                        X
               CRDERR=RETRY,                                           X
               RDONLY=YES,                                             X
               MODNAME=IJCFZNB4,                                       X
               WORKA=YES
IJCFZNB4 CDMOD IOAREA2=YES,                                            X
               RDONLY=YES,                                             X
               TYPEFLE=OUTPUT,                                         X
               CRDERR=RETRY,                                           X
               WORKA=YES
SYSOUT   DTFPR   DEVADDR=SYSLST,                                       X
               IOAREA1=LINE1,                                          X
               IOAREA2=LINE2,                                          X
               BLKSIZE=133,                                            X
               CTLCHR=ASA,                                             X
               MODNAME=IJDFAZIY,                                       X
               RDONLY=YES,                                             X
               IOREG=(3)
IJDFAZIY PRMOD CTLCHR=ASA,                                             X
               IOAREA2=YES,                                            X
               RDONLY=YES
LINE1    DS      133C
LINE2    DS      133C
SYSOUTLC DS    F
         END
