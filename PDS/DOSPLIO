TPIO     TITLE 'DOS PL360 TAPE AND DISC I/O ROUTINES'
*"The source code for this computer program is placed in the public
* domain and may be used by any party without notice to the copyright
* holder, Stanford University. Stanford University provides no
* support of any kind to this computer program.  Further:
*
* STANFORD MAKES NO REPRESENTATIONS OR WARRANTIES, EXPRESS OR IMPLIED.
* BY WAY OF EXAMPLE, BUT NOT LIMITATION, STANFORD MAKES NO
* REPRESENTATIONS OR WARRANTIES OF MERCHANTABILITY OR FITNESS FOR ANY
* PARTICULAR PURPOSE OR THAT THE USE OF THE SOFTWARE WILL NOT INFRINGE
* ANY PATENTS, COPYRIGHTS, TRADEMARKS, OR OTHER RIGHTS. STANFORD SHALL
* NOT BE LIABLE FOR ANY LIABILITY OR DAMAGES WITH RESPECT TO ANY CLAIM
* BY A USER OF THIS COMPUTER PROGRAM OR ANY DERIVATIVES OF THIS
* COMPUTER PROGRAM."
         MACRO
&EP      ENTER
         SPACE
         ENTRY &EP
         USING &EP,15
&EP      STM   12,3,SAVE
         L     12,=A($TAPEIO)
         USING $TAPEIO,12
         DROP  15
         SPACE
         MEND
         SPACE 2
         MACRO
&L       EXIT
         SPACE
&L       LM    2,3,SAVE+24
         LM    12,0,SAVE
         BR    14
         DROP  12
         SPACE 2
         MEND
         SPACE 2
$TAPEIO  CSECT
         SPACE
*  GLOBAL PROCEDURE  OPEN(R14);
*    (R1) = DTF @
*    (R13) = SAVE AREA @
*    (R14) = RETURN @
         SPACE
OPEN     ENTER
         L     2,LASTNTRY
         ST    1,0(2)
         OPEN  (1)
         L     1,LASTNTRY
         ST    2,4(1)
         LA    1,8(1)
         ST    1,LASTNTRY
         LM    12,3,SAVE
         BR    14
         DROP  12
*  GLOBAL PROCEDURE  GET(R14);
*    (R1) = DTF @
*    (R13) = SAVE AREA @
*    (R14) = RETURN @
         SPACE
GET      ENTER
         LA    3,TABLE
LOOK1    C     1,0(3)
         BE    GET0
         LA    3,8(3)
         C     3,LASTNTRY
         BNE   LOOK1
         DUMP
GET0     GET   (1)
         LR    1,2
GET1     CLI   0(3),0
         EXIT
         USING $TAPEIO,12
         ENTRY ENDRDR
ENDRDR   MVI   0(3),X'FF'              INDICATE EOF
         B     GET1
         DROP  12
         SPACE 2
*  GLOBAL PROCEDURE  PUT(R14);
*    (R1) = DTF @
*    (R13) = SAVE AREA @
*    (R14) = RETURN @
         SPACE
PUT      ENTER
         ST    1,DTFLOC
         LA    3,TABLE
LOOK2    C     1,0(3)
         BE    PUT0
         LA    3,8(3)
         C     3,LASTNTRY
         BNE   LOOK2
         DUMP
PUT0     CLI   4(3),X'F0'
         BE    PUT1
         MVI   4(3),X'F0'
         L     1,4(3)
         B     PUT2
PUT1     PUT   (1)
         LR    1,2
*  NOW SEE IF VARIABLE RECORDS ARE BEING WRITTEN
         L     2,DTFLOC
         TM    20(2),X'10'             TEST IF DTFMT
         BNO   SDTST
         TM    36(2),X'02'             TEST IF VARIABLE RECORDS
         BNO   PUT2
*  VARBLD=(3) MUST BE SPECIFIED
         C     3,72(2)                 TEST IF ENUF ROOM IN BUFFER
         BNL   PUT2
         LR    1,2
         TRUNC (1)
         L     1,DTFLOC
         PUT   (1)
         B     PUT2
SDTST    TM    20(2),X'20'             TEST IF DTFSD
         BNO   PUT2
         TM    100(2),X'02'            TEST IF NOT VARIABLE RECORDS
         BO    PUT2
         C     3,92(2)                 TEST IF ENUF ROOM IN BUFFER
         BNL   PUT2
         LR    1,2
         TRUNC (1)
         L     1,DTFLOC
         PUT   (1)
PUT2     EXIT
*  GLOBAL PROCEDURE  KLOSE(R14);
*    (R1) = DTF @
*    (R13) = SAVE AREA @
*    (R14) = RETURN @
         SPACE
KLOSE    ENTER
         LA    3,TABLE
LOOK3    C     1,0(3)
         BE    CLZ0
         LA    3,8(3)
         C     3,LASTNTRY
         BNE   LOOK3
         DUMP
CLZ0     CLI   4(3),X'F0'
         BNE   CLZ1
         PUT   (1)
         L     1,SAVE+20               GET DTF @                      +
CLZ1     CLOSE (1)
         MVI   0(3),X'F0'              INDICATE FILE IS CLOSED
         LM    12,3,SAVE
         BR    14
         DROP  12
SAVE     DS    8F
DTFLOC   DS    F
EOF      DC    X'00'
TABLE    DC    60F'0'                  LIMIT OF 30 DIFFERENT FILES
LASTNTRY DC    A(TABLE)
         END
