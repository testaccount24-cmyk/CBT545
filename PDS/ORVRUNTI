// JOB ,'ORV.RUNTIME',CLASS=E
/*JOBPARM  LINES=3,TIME=4
//* ORVYL INTERFACE FOR PL360 RUNTIME ROUTINES
//ASM  EXEC  ASMORVYL,ASM=ASMA90,PARM='USING(WARN(11))'
//ASM.SYSLIN DD SYSOUT=B
//ASM.SYSIN DD *
PLIN     TITLE    'ORVYL INTERFACE FOR PL360 RUNTIME PROGRAMS'
*"The source code for this computer program is placed in the public
* domain and may be used by any party without notice to the copyright
* holder, Stanford University. Stanford University provides no
* support of any kind to this computer program.  Further:
*
* STANFORD MAKES NO REPRESENTATIONS OR WARRANTIES, EXPRESS OR IMPLIED.
* BY WAY OF EXAMPLE, BUT NOT LIMITATION, STANFORD MAKES NO
* REPRESENTATIONS OR WARRANTIES OF MERCHANTABILITY OR FITNESS FOR ANY
* PARTICULAR PURPOSE OR THAT THE USE OF THE SOFTWARE WILL NOT INFRINGE
* ANY PATENTS, COPYRIGHTS, TRADEMARKS, OR OTHER RIGHTS. STANFORD SHALL
* NOT BE LIABLE FOR ANY LIABILITY OR DAMAGES WITH RESPECT TO ANY CLAIM
* BY A USER OF THIS COMPUTER PROGRAM OR ANY DERIVATIVES OF THIS
* COMPUTER PROGRAM."
         MACRO
&L       PENTER
         ENTRY &L
         USING &L,15
&L       STM   0,15,SAVE
         BALR  BASER,0
         USING *,BASER
         DROP  15
         LA    R13,STACK
         MEND
         MACRO
&L       UCALL &NAME
&L       L     RETURN,=A(&NAME)
         BALR  RETURN,RETURN
         MEND
         MACRO
&L       UENTER
&L       STM   R1,R0,0(R13)
         BALR  BASER,0
         USING *,BASER
         AL    R13,BUMP13
         MEND
         MACRO
&L       UEXIT &RST,&LTR
&L       SL    R13,BUMP13
         AIF   ('&RST' EQ '').MAX
         LM    R3,R0,8(R13)
         AGO   .NMAX
.MAX     LM    R1,R0,0(R13)
.NMAX    AIF   ('&LTR' EQ '').NLTR
         LTR   &LTR,&LTR
.NLTR    BR    RETURN
         EJECT
         MEND
         MACRO
&L       ABORT &CODE
&L       LA    R3,&CODE
         UCALL ABORT
         MEND
         EJECT
*
**REGISTER USAGE
*
R0       EQU   0                  LOCAL WORK ONLY..USED BY SVC
R1       EQU   1                  LOCAL WORK ONLY..USED BY SVC
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10                 LOCAL BASE REGISTER
R11      EQU   11
R12      EQU   12                 BASE TO HOUSE KEEPING DSECT
R13      EQU   13                 POINTER TO NEXT FREE SAVE AREA
R14      EQU   14                 LOCAL WORK ONLY..USED BY SVC
R15      EQU   15                 LOCAL WORK ONLY..USED BY SVC
*
BASER    EQU   R10                LOCAL BASE REGISTER
RETURN   EQU   R14                LOCAL RETURN REGISTER
         EJECT
*
PL360    CSECT
*
**GET ADDRESSABILITY
*
         BALR  BASER,0            SET UP BASE REG
         USING *,BASER            TELL ASSEMBLER
         LA    R13,SAVE1
         ATTACH 1,0               ATTACH USERS TERMINAL
         ST    R0,TI              DROP TERMINAL IDENTIFIER INTO CORE
         WAIT  (0)                WAIT FOR ATTACH TO COMPLETE
         EJECT
*
**SET WYLBUR MODE WORD AND ESTABLISH MAGIC WORD TO READ WDS
*
         CONTROL TI,12,0  NON-PREST FORMAT  ALL OTHER GO
         WAIT  (0)                WAIT FOR WORD TO BE SET
*
**SENSE STORAGE BOUNDS
*
         SET   14,L:MINSPAC,0
         BZ    GOON
         PANIC 200
GOON     LR    R2,R0
         AR    R2,R1
** LOW BOUND IN R1, HIGH BOUND IN R2
         LA    R0,2048
         AR    R0,R0              ; 4096 BUFFERS
         ST    R1,CBUF
         ST    R1,CBPTR
         AR    R1,R0
         ST    R1,CBUFEND
         ST    R1,LBUF
         ST    R1,LBPTR
         AR    R1,R0
         ST    R1,LBUFEND
*
**CALL USER PROGRAM
*
         L     R15,=V(SEGN001)
         BALR  R14,R15
EXIT     UENTER
         L     R15,CBPTR
         L     R1,CBUF
         CR    R15,R1
         BE    STRM0
*
*  PUNCH END OF OBJECT MODULE
*
         SR    R15,R1
         L     R14,RECNO
         L     R0,OBJ
         L     R5,=A(FORCEDI)
         BALR  R5,R5
         WRITE (0),(1),(15),(14),MODE=WAIT
         WAIT  (0)
         BZ    STRM0
         ABORT 100
         UCALL EXIT
STRM0    L     R15,LBPTR
         L     R1,LBUF
         CR    R1,R15
         BE    STRM1
*
*  WRITE END OF LISTING
*
         SR    R15,R1
         L     R14,LRECNO
         L     R0,LINEP
         L     R5,=A(FORCEDI)
         BALR  R5,R5
         WRITE (0),(1),(15),(14),MODE=WAIT
         WAIT  (0)
         BZ    STRM1
         ABORT 100
STRM1    SR    R1,R1
         C     R1,OBJ
         BE    XIT1
         DETACH OBJ
         WAIT  (0)
         SR    R1,R1
XIT1     C     R1,LINEP
         BE    XIT2
         DETACH LINEP
         WAIT  (0)
XIT2     SYSQS R1,R2,'-END EXECUTION'
         UCALL TPUT
         EXIT
         EJECT
PRINT    PENTER
         LR    R3,R0
         CLI   LINEPFG,X'FF'
         BE    PRT0
         LA    R1,PRNAME
         UCALL ATCHFL
         MVC   LINEP(4),FILEDI
         MVI   LINEPFG,X'FF'
         SR    R14,R14
         L     R15,FILESZ
         D     R14,=F'133'
         M     R14,=F'133'
         ST    R15,LINEPSZ
         A     R15,LBUF
         ST    R15,LBUFEND
         L     R15,=X'04000085'   LRECL=133
         CONTROL LINEP,14,(15),MODE=(FORMAT,WAIT)
         WAIT  (0)
PRT0     SR    R1,R1
         C     R1,LINEP
         BE    PRINTX
         L     R2,LBPTR
         C     R2,LBUFEND
         BNE   PRT1
         L     R1,LBUF
         L     R14,LRECNO
         L     R0,LINEP
         L     R5,=A(FORCEDI)
         BALR  R5,R5
         L     R15,LINEPSZ
         WRITE (0),(1),(15),(14),MODE=WAIT
         WAIT  (0)
         BZ    PRT2
         ABORT 100
PRT2     L     R1,LRECNO
         LA    R1,1(R1)
         ST    R1,LRECNO
         L     R2,LBUF
         ST    R2,LBPTR
PRT1     MVC   0(133,R2),0(R3)
         L     R1,LBPTR
         LA    R1,133(R1)
         ST    R1,LBPTR
PRINTX   LM    0,15,SAVE
         BR    14
LINEPFG  DC    X'00'
PRNAME   DC    C'PRINT'
LINEP    DC    F'0'
LINEPSZ  DC    F'2048'
LRECNO   DC    F'0'
         EJECT
PUNCH    PENTER
         LR    R3,R0
         CLI   OBJFLAG,X'FF'
         BE    PUNCH0
         LA    R1,PUNAME
         UCALL ATCHFL
         MVC   OBJ(4),FILEDI
         MVI   OBJFLAG,X'FF'
         SR    R14,R14
         L     R15,FILESZ
         D     R14,=F'80'
         M     R14,=F'80'
         ST    R15,OBJSZ
         A     R15,CBUF
         ST    R15,CBUFEND
         L     R15,=X'04000050'   LRECL=80
         CONTROL OBJ,14,(15),MODE=(FORMAT,WAIT)
         WAIT  (0)
PUNCH0   SR    R1,R1
         C     R1,OBJ
         BE    PUNCH3
         L     R2,CBPTR
         C     R2,CBUFEND
         BNE   PUNCH2
         L     R1,CBUF
         L     R14,RECNO
         L     R0,OBJ
         L     R5,=A(FORCEDI)
         BALR  R5,R5
         L     R15,OBJSZ
         WRITE (0),(1),(15),(14),MODE=WAIT
         WAIT  (0)
         BZ    PUNCH1
         ABORT 100
PUNCH1   L     R1,RECNO
         LA    R1,1(R1)
         ST    R1,RECNO
         L     R2,CBUF
         ST    R2,CBPTR
PUNCH2   MVC   0(80,R2),0(R3)
         L     R1,CBPTR
         LA    R1,80(R1)
         ST    R1,CBPTR
PUNCH3   LM    0,15,SAVE
         BR    14
OBJFLAG  DC    X'00'
PUNAME   DC    C'PUNCH'
OBJ      DC    F'0'
OBJSZ    DC    F'2048'
RECNO    DC    F'0'
         DROP  BASER
         SPACE
*
**ROUTINE TO FORCE FILE DIRECTORY
*
FORCEDI  STM   R1,R0,0(R13)
         BALR  BASER,0
         USING *,BASER
         AL    R13,BUMP13
         CONTROL (0),0,(14),MODE=WAIT
         BNZ   FRCERR
         WAIT  (0)
         BNZ   FRCERR
         CONTROL (0),7,(15)
         BNZ   FRCERR
         WAIT  (0)
         BNZ   FRCERR
         SL    R13,BUMP13
         LM    R1,R0,0(R13)
         BR    R5
FRCERR   ABORT 100
         DROP  BASER
         EJECT
ATCHFL   UENTER
         MVC   FILENAME+3(5),0(R1)
         LA    R1,FILENAME
         LA    R6,8
         LR    R15,R6
AT0      ATTACH 2,(1),(15)
         BZ    AT2
         ABORT 8
AT2      ST    R0,FILEDI
         WAIT  (0)
         BZ    ATCHFLX
         SR    R0,R0
         ST    R0,FILEDI
         C     R15,=F'20'
         BNE   AT3
         LA    R1,FILENAME
         LA    R2,24
         MVI   ATTNFG,X'00'
         UCALL TPUT
         LA    R2,SCROK
         UCALL YESTEST
         BNZ   AT6
         UCALL EXIT
AT6      ATTACH 2,FILENAME,(6),MODE=(OLD)
         BZ    AT7
         ABORT 8
AT7      ST    R0,FILEDI
         WAIT  (0)
         BNZ   AT3
         CONTROL FILEDI,8,(15)
         BZ    AT4
         ABORT 72
AT4      WAIT  FILEDI
         BZ    AT5
         ABORT 76
AT5      LR    R15,R6
         LA    R1,FILENAME
         B     AT0
AT3      ABORT 28
FILEDI   DC    F'0'
FILESZ   DC    F'2048'
SCROK    DC    F'9'
         DC    C'REPLACE? '
FILENAME DC    C'SYSXXXXX ALREADY EXISTS.'
ATCHFLX  ST    R0,FILEDI
         LA    R3,2048
         CONTROL (0),23
         BNE   ATCHFX
         WAIT  (0)
         BNE   ATCHFX
         CR    R1,R3
         BNH   ATCHFX
         LR    R3,R1
ATCHFX   ST    R3,FILESZ
         UEXIT ,
YESTEST  UENTER
*  ON ENTRY, (R2)= @ PROMPT
*  IF THE USER RESPONSE IS "YES" THEN R1=1 ON RETURN,
*  ELSE IF "NO" THEN R1=0, ELSE NO RETURN.
*
         LA    R1,INPUT
YES0     UCALL TPROMPT
         C     2,=F'3'
         BNH   YES1
         LA    R2,3
YES1     BCTR  R2,0
         EX    R2,COMP1
         BE    YESOUT
         C     R2,=F'1'
         BNH   YES2
         LA    R2,1
YES2     EX    R2,COMP2
         BE    NOOUT
         EX    R2,COMP3
         BE    YESOUT
         LA    R1,INPUT
         LA    R2,YESORNO
         B     YES0
YESOUT   LA    R1,1
         B     YESXIT
NOOUT    LA    R1,0
         B     YESXIT
COMP1    CLC   0(0,1),=C'YES'
COMP2    CLC   0(0,1),=C'NO'
COMP3    CLC   0(0,1),=C'OK'
YESXIT   SL    R13,BUMP13
         LM    R2,R0,4(R13)
         LTR   R1,R1
         BR    RETURN
         DROP  BASER
YESORNO  DC    F'23'
         DC    C'PLEASE TYPE YES OR NO: '
         EJECT
ABORT    UENTER
         AR    R3,R15
         XR    R0,R0
         EX    R0,ABORTMSG(R3)
         LA    R2,LENGTH
         UCALL TPUT
         UCALL EXIT
ABORTMSG LA    R1,=C'ILLEGAL NAME          '
         LA    R1,=C'EXCESS DEVICES        '
         LA    R1,=C'READ PAST ATTENTION   '
         LA    R1,=C'ALREADY IN USE        '
         LA    R1,=C'ALREADY IN USE        '
         LA    R1,=C'ALREADY IN USE        '
         LA    R1,=C'ALREADY IN USE        '
         LA    R1,=C'IMPOSSIBLE ERROR      '
         LA    R1,=C'ACCESS NOT PERMITTED  '
         LA    R1,=C'FILE STORAGE EXCEEDED '
         LA    R1,=C'DIRECTORY FULL        '
         LA    R1,=C'FILE NOT THERE        '
         LA    R1,=C'FILE ALREADY THERE    '
         LA    R1,=C'TABLE OVERFLOW        '
         LA    R1,=C'DISK SPACE EXHAUSTED  '
         LA    R1,=C'NOT ACCESSIBLE        '
         LA    R1,=C'ILLEGAL NAME          '
         LA    R1,=C'ALREADY IN USE        '
         LA    R1,=C'I/O ERROR             '
         LA    R1,=C'IMPOSSIBLE ERROR      '
         LA    R1,=C'ALREADY IN USE        '
         LA    R1,=C'ACCESS NOT PERMITTED  '
         LA    R1,=C'DUPLICATE NAME        '
         LA    R1,=C'DIRECTORY FULL        '
         LA    R1,=C'I/O ERROR             '
         LA    R1,=C'IMPOSSIBLE ERROR      '
         LA    R1,=C'ALREADY IN USE        '
         LA    R1,=C'ACCESS NOT PERMITTED  '
         LA    R1,=C'RECORD ALREADY EXISTS '
         LA    R1,=C'FILE STORAGE EXCEEDED '
         LA    R1,=C'DISK SPACE EXHAUSTED  '
LENGTH   EQU   22
         EJECT
         DROP  BASER
         ENTRY PAGE
PAGE     BR    R14
WRITE    PENTER
         LR    R1,R0
         LA    R3,131(R1)
         LR    R5,R1
         BCTR  R5,0
         L     R4,=F'-1'
WRITE1   CLI   0(R3),C' '
         BNE   WRITE2
         BXH   R3,R4,WRITE1
WRITE2   SR    R3,R5
         LR    R2,R3
         UCALL TPUT
         LM    R0,R15,SAVE
         BR    R14
         DROP  BASER
         SPACE
*
TPUT     UENTER
         CLI   ATTNFG,X'FF'       CHECK FOR ATTENTION
         BE    TPUTX              JUMP IF ATTENTION OCCURED
         WRITE TI,(1),(2)
         WAIT  (0)                WAIT FOR GOOD COMPLETION
         BNH   TPUTX              JUMP IF NO ATTENTION
         MVI   ATTNFG,X'FF'       SET ATTENTION FLAG
TPUTX    UEXIT ,                  RETURN TO CALLER
READ     PENTER
         CLI   EOF,X'00'
         BE    READ1
         SR    R15,R15
         ABORT 8
READ1    LR    R1,R0
         LR    R2,R1
         UCALL TGET
         LTR   R15,R2
         BNE   READX
         MVI   EOF,X'FF'
READX    CLI   EOF,X'00'
         LM    R0,R14,SAVE
         BR    R14
         DROP  BASER
         EJECT
*
**SPECIAL PROMPT TERMINAL INPUT ROUTINE
*
*ON ENTRY R1 = @UPPER INPUT REGION.
*         R2 = @PROMPT.
*ON EXIT R1 = @UPPER INPUT REGION.
*        R2 = # OF CHARACTERS IN THE INPUT LINE.
*
TPROMPT  UENTER
         LR    R5,R2                     R5 := R2;
         LR    R2,R1                     R2 := R1;
         LR    R8,R1                     R8 := R1; COMMENT R8 ¬= 0;
         B     TGET0                     GOTO TGET0;
*
**TERMINAL INPUT ROUTINE
*
*ON ENTRY R1 = @UPPER INPUT REGION.
*         R2 = @UPLOW INPUT REGION.
*
*ON EXIT R1 = @UPPER INPUT REGION.
*        R2 = # OF CHARACTERS IN THE INPUT LINE.
*
TGET     UENTER
* IF R1=R2 ON ENTRY, ONLY UPPER CASE INPUT IS RETURNED.
*
         LA    R5,PROMPT                 R5 := @PROMPT;
         XR    R8,R8                     R8 := 0;
TGET0    BALR  BASER,0                TGET0: BALR(BASER,0);
         USING *,BASER
         LR    R3,R1                     R3 := R1;
         LR    R4,R2                     R4 := R2;
TGET1    MVI   ATTNFG,X'00'           TGET1: RESET(ATTNFG);
TGET2    MVI   0(R4),C' '             TGET2: MVI(" ",B4);
         MVC   1(79,R4),0(R4)            MVC(78,B4(1),B4);
         READ  TI,(4),L:TIME133,(5)      READ(TI);
         WAIT  (0)                       WAIT(TI);
         BZ    TGET7                     IF = THEN GOTO TGET7;
         BM    TGET4                     IF < THEN GOTO TGET4;
         BC    15,*(R15)                 BRANCH(ON,SWITCH(R15));
         B     TGET3                     GOTO TGET3;
         B     TGET2                     GOTO TGET2;
TGET4    LCR   R15,R15                TGET4: R15 := NEG R15;
         BC    15,*(R15)                 BRANCH(ON,SWITCH(R15));
         B     TGET5                     GOTO TGET5; COMMENT I/O ERR;
         SYSQS R1,R2,'-ILLEGAL USE OF TABS.'
         B     TGET6                     GOTO TGET6;
TGET5    SYSQS R1,R2,'-TRANSMISSION ERROR, RETYPE.'
TGET6    UCALL TPUT                   TGET6: TPUT('ERROR MESSAGE');
         B     TGET1                     GOTO TGET1;
* R1 CONTAINS LINE LENGTH
TGET7    LTR   R2,R1                  TGET7: R2 := R1;
         BZ    TGET1                     IF R2 = 0 THEN GOTO TGET1;
TGET9    LR    R7,R2                  TGET9: R7 := R2;
         BCTR  R7,0                      REDUCE(R7);
TGET10   CR    R4,R3                  TGET10: IF R4=R3 THEN
         BE    TGET11                               GOTO TGET11;
         EX    R7,MOVIN2                 EX(R7,MOVIN2);
TGET11   EX    R7,TOUPPER             TGET11: EX(R7,TOUPPER);
         B     TGETX                     GOTO TGETX;
MOVIN1   MVC   0(0,R4),0(R6)
MOVIN2   MVC   0(0,R3),0(R4)
TOUPPER  OC    0(0,R3),BLANKCS
TIME133  DS    0F
         DC    H'0'              WAS 300 TO ALLOW 5 MINUTE TIME-OUT
         DC    H'133'
PROMPT   DC    F'3'
         DC    C'-? '
TGET3    LTR   R1,R1                  TGET3: IF R1 ¬= 0 THEN
         BNZ   TGET1                        GOTO TGET1;
         SR    R2,R2                     R2 := R2-R2;
TGETX    LR    R1,R3                  TGETX: R1 := R3;
         MVI   ATTNFG,X'00'              RESET(ATTNFG);
         UEXIT *,R2                      UEXIT(*);
** LOCAL CONSTANTS
*
BUMP13   DC    F'64'
MINSPAC  DC    A(4096*3)
*
** ROUTINE LITERAL POOL
*
         LTORG
BLANKCS  DC    256X'40'
ATTNFG   DC    X'00'
EOF      DC    X'00'
CBUF     DS    F
CBPTR    DS    F
CBUFEND  DS    F
LBUF     DS    F
LBPTR    DS    F
LBUFEND  DS    F
TI       DS    F
SAVE1    DS    18F
SAVE     DS    16F
STACK    DS    128F
INPUT    DS    152C
         END
/*
